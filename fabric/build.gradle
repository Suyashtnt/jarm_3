import com.modrinth.minotaur.TaskModrinthUpload

plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id 'org.jetbrains.kotlin.jvm'
}

repositories {
    maven {
        url = "http://maven.fabricmc.net/"
        name = "Fabric"
    }
    mavenCentral()
}

Map<String, String> ENV = System.getenv()

configurations {
    shadow
}

architectury {
    platformSetupLoomIde()
}

dependencies {
    minecraft("com.mojang:minecraft:${rootProject.architect.minecraft}")
    mappings(minecraft.officialMojangMappings())
    modCompile("net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}")
    modCompile("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}")

    // Remove the next line if you don't want to depend on the API
    modCompile("me.shedaniel:architectury:${rootProject.architectury_version}:fabric")
    modImplementation("net.fabricmc:fabric-language-kotlin:1.4.21+build.1")

    compileOnly(project(path: ":common")) {
        transitive = false
    }
    runtimeOnly(project(path: ":common", configuration: "transformedRuntime")) {
        transitive = false
    }
    shadow(project(path: ":common", configuration: "transformed")) {
        transitive = false
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
    inputs.property "version", project.version
}

shadowJar {
    configurations = [project.configurations.shadow]
    classifier "shadow"
}

remapJar {
    dependsOn(shadowJar)
    input.set(shadowJar.archivePath)
    archiveClassifier = "fabric"
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifact(remapJar.archivePath) {
                builtBy build
                classifier "fabric"
            }
        }
    }
}


task publishModrinth (type: TaskModrinthUpload) {
    dependsOn("build")
    token = ENV['MODRINTH_TOKEN']
    projectId = 'kuED0e2M'
    versionNumber = rootProject.mod_version+"-fabric"
    uploadFile = remapJar
    addGameVersion(rootProject.minecraft_version)
    addLoader('fabric')
    changelog = '''
    That damn TABS dude forgot to update the changelog
    '''
    releaseType = "alpha"
}

rootProject.tasks.getByName("modrinthPublish").dependsOn tasks.getByName("publishModrinth")
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
